class SnakeGame {
    field Snake snake;
    static int direction;  //current direction

    /** Constructs a new Square Game. */
    constructor SnakeGame new() {
        var int size;
        let size = 16;
        let snake = Snake.new(Square.new(0, 0, size), null);
        let snake = Snake.new(Square.new(1, 0, size), snake);
        let snake = Snake.new(Square.new(2, 0, size), snake);

        let direction = 1; // right
        do snake.setDirection(direction);

        return this;
    }

    /** Disposes this game. */
    method void dispose() {
        do snake.dispose();
        return;
    }

    /** Runs the game: handles the user's inputs and moves the square accordingly */
    method void run() {
        var int timer;
        var char key;  // the key currently pressed by the user
        var int d;
        var boolean exit;
        let timer = 0;
        let exit = false;
        let d = direction;

        while (~exit) {
            let key = Keyboard.keyPressed();

            if (key = 81)  { let exit = true; }             // q key
            if (key = 132) {                                // right arrow
                if (~(direction = -1)) {
                    let d = 1;
                }
            }
            if (key = 130) {                                // left arrow
                if (~(direction = 1)) {
                    let d = -1;
                }
            }
            if (key = 133) {                                // down arrow
                if (~(direction = 2)) {
                    let d = -2;
                }
            }
            if (key = 131) {                                // up arrow
                if (~(direction = -2)) {
                    let d = 2;
                }
            }

            if (timer > 60) {
                let direction = d;
                do snake.setDirection(direction);
                let snake = snake.move();
                let timer = 0;
            }

            do Sys.wait(5);     // delays the next movement
            let timer = timer + 1;
        }

        return;
    }
}
